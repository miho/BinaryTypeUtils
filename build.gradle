import java.text.SimpleDateFormat

plugins {
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.44.0'
}

plugins {
    id 'java-library'
}

ext.commonProps = new Properties()
rootProject.file("config/common.properties").withInputStream { commonProps.load(it) }

apply from: 'gradle/publishing.gradle'

wrapper {
    gradleVersion = '7.6'
}

sourceCompatibility = '17'
targetCompatibility = '17'
[compileJava, compileTestJava]*.sourceCompatibility = '17'
[compileJava, compileTestJava]*.targetCompatibility = '17'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"   << "-Werror"
    options.compilerArgs << "-Xlint:deprecation" << "-Werror"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13'

    implementation 'eu.mihosoft.streamutils:streamutils:0.1.6'

    implementation "org.tinylog:tinylog-api:2.5.0"
    implementation "org.tinylog:tinylog-impl:2.5.0"
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'eu.mihosoft.binarytypeutils')
    }
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = true

    mapping {
        java   = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
        fxml   = 'XML_STYLE'
    }

    ext.yearCurrent  = new SimpleDateFormat("yyyy").format(new Date())
    ext.yearSince1   = '2019'
    ext.author1 = 'Michael Hoffer <info@michaelhoffer.de>'

    exclude '**/*.svg'
}
